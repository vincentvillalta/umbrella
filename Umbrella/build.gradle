buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.2.3'
    classpath 'me.tatarka:gradle-retrolambda:3.4.0'
    classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
  }
}

repositories{
  jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.ben-manes.versions'

// Use `com.foo.umbrella.weatherundergroundapikey` in gradle.properties or as a command line
// parameter or `WEATHER_UNDERGROUND_API_KEY` as a System Environment variable to provide weather
// underground api key to this project
ext.weatherUndergroundApiKey = project.hasProperty('com.foo.umbrella.weatherundergroundapikey')?
    project.getProperty('com.foo.umbrella.weatherundergroundapikey'):
    System.getenv('WEATHER_UNDERGROUND_API_KEY')?:''

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.1'

  signingConfigs {
    umbrella {
      storeFile file('debug.keystore')
      storePassword 'android'
      keyAlias 'androiddebugkey'
      keyPassword 'android'
    }
  }

  defaultConfig {
    applicationId "com.foo.umbrella"
    minSdkVersion 19
    targetSdkVersion 25
    versionCode 1
    versionName '1.0'

    buildConfigField "String", "API_KEY", "\"$project.weatherUndergroundApiKey\""
    buildConfigField "String", "API_URL", "\"http://api.wunderground.com\""

    signingConfig signingConfigs.umbrella
  }

  buildTypes {
    debug{
      applicationIdSuffix'.debug'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  compile 'com.android.support:support-v4:25.1.0'
  compile 'com.android.support:support-annotations:25.1.0'
  compile 'com.android.support:appcompat-v7:25.1.0'
  compile 'com.android.support:recyclerview-v7:25.1.0'
  compile 'com.android.support:design:25.1.0'
  compile 'com.android.support:cardview-v7:25.1.0'

  provided 'com.google.auto.value:auto-value:1.3'
  provided 'com.ryanharter.auto.value:auto-value-moshi-annotations:0.4.2'
  annotationProcessor 'com.ryanharter.auto.value:auto-value-moshi:0.4.2'

  compile 'com.squareup.okhttp3:okhttp:3.5.0'
  compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'com.squareup.retrofit2:retrofit:2.1.0'
  compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
  compile 'com.squareup.retrofit2:converter-moshi:2.1.0'
  compile 'com.squareup.moshi:moshi:1.3.1'

  compile 'io.reactivex:rxjava:1.2.5'
  compile 'io.reactivex:rxandroid:1.2.1'

  compile 'com.jakewharton.byteunits:byteunits:0.9.1'
  compile 'com.jakewharton.threetenabp:threetenabp:1.0.4'
  compile 'com.jakewharton.timber:timber:4.5.1'
  compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

  compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'

  debugCompile 'com.facebook.stetho:stetho:1.4.2'
  debugCompile 'com.facebook.stetho:stetho-okhttp3:1.4.2'
  debugCompile 'com.facebook.stetho:stetho-timber:1.4.2@aar'
}
